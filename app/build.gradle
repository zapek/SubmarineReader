/**
 * Copyright 2017 by David Gerber, Zapek Software Engineering
 * https://zapek.com
 *
 * This file is part of Submarine Reader.
 *
 * Submarine Reader is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Submarine Reader is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Submarine Reader.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'

/* This is used for private (non GPL) builds */
if (file('external.gradle').exists())
{
	apply from: 'external.gradle'
}

android {
	signingConfigs {
		saker {
			storeFile file(KEYSTORE_ZAPEK_STORE_FILE)
			storePassword KEYSTORE_ZAPEK_STORE_PASSWORD
			keyAlias KEYSTORE_SAKER_KEY_ALIAS
			keyPassword KEYSTORE_SAKER_KEY_PASSWORD
		}
	}
	compileSdkVersion 25
	buildToolsVersion '25.0.2'

	/* default configuration */
	defaultConfig {
		minSdkVersion 16
		targetSdkVersion 25
		vectorDrawables.useSupportLibrary = "true"
		buildConfigField "boolean", "logging", "true"
		buildConfigField "String", "IAB_KEY", "\"\""
		buildConfigField "String", "ACRAUri", "\"\""
		resValue "string", "development_url", ""
		resValue "string", "slogan", ""
		testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
	}
	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
			buildConfigField "boolean", "logging", "false"
		}
		/* 'beta' is not for the Play Store, only private betas */
		beta {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
			applicationIdSuffix ".beta"
			versionNameSuffix "-beta"
		}
	}
	productFlavors {
		saker {
			applicationId 'is.thesaker.reader'
			signingConfig signingConfigs.saker
			versionCode 2
			versionName "1.1"
			buildConfigField "String", "serverUrl", "\"http://thesaker.is/wp-json/wp/v2/\""
			buildConfigField "String", "ACRAUri", "\"https://zapek.com/report/saker/\""
			buildConfigField "String", "IAB_KEY", "\"MIIBIjANBgkqhkiG9w0BAQEFAAOCAQ8AMIIBCgKCAQEArNnrAa1goufmIdR+/lmiCagRO5j75BDL0TXBVhTSGuBhNXJJKfx4Tq9OoJzVGDNWy/9UpGtD0gZiFA4n1eIbjNcKKhEabB6ab3E/BA/UnKQY97gwVt5GenT55YuFaxv8CUZqMyGRmjFmyP4H0lBBdAG2WZqwgW/59Kz/xRJV2ro/IyD70aZlqrHHYz8TSD2DZ8DHaNQUHdwIhTdBs3YwzQwPebOSFhH2mYGU9sP3c47GCf1jESx6wDZyyHBy3Ee3FOkOodmkLky8fDfh56Gr58/NyzoaIwUwVIfyaXOnjbS5031UFgJcpGnb05DTA77+L74qB0Yy4/awHjA+ZBVPNQIDAQAB\""
			resValue "string", "app_name", "The Saker"
			resValue "string", "slogan", "Stop the Empire\\'s war on Russia"
			resValue "string", "web_url", "http://thesaker.is"
			resValue "string", "support_email", "vineyardsaker@gmail.com"
			resValue "string", "development_url", "https://zapek.com"
		}
	}

	// productFlavors are run before buildTypes hence they don't use the final
	// applicationId. This final loop fixes it
	applicationVariants.all { variant ->
		variant.buildConfigField "String", "providerAuthority", "\"${applicationId}\""
		variant.buildConfigField "String", "accountType", "\"${applicationId}\""
		variant.buildConfigField "String", "accountName", "\"Public\""
		variant.resValue "string", "application_id", applicationId
	}
}

dependencies {
	compile fileTree(include: ['*.jar'], dir: 'libs')
	androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
		exclude group: 'com.android.support', module: 'support-annotations'
	})
	compile 'com.android.support:appcompat-v7:25.3.1'
	compile 'com.android.support:design:25.3.1'
	compile 'com.android.support:cardview-v7:25.3.1'
	compile 'com.squareup.okhttp3:okhttp:3.7.0'
	compile 'com.squareup.retrofit2:retrofit:2.2.0'
	compile 'com.squareup.retrofit2:converter-gson:2.2.0'
	compile 'com.google.code.gson:gson:2.8.0'
	compile 'org.jsoup:jsoup:1.10.2'
	compile 'com.squareup.picasso:picasso:2.5.2'
	compile 'jp.wasabeef:picasso-transformations:2.1.2'
	compile 'jp.co.cyberagent.android.gpuimage:gpuimage-library:1.4.1'
	betaCompile 'ch.acra:acra:4.9.2'
	testCompile 'junit:junit:4.12'
}
