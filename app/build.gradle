/**
 * Copyright 2017-2019 by David Gerber, Zapek Software Engineering
 * https://zapek.com
 *
 * This file is part of Submarine Reader.
 *
 * Submarine Reader is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Submarine Reader is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Submarine Reader.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'

/* This is used for private (non GPL) builds */
if (file('external.gradle').exists())
{
	apply from: 'external.gradle'
}

android {
	flavorDimensions "main"
	dataBinding.enabled = true

	if (project.hasProperty("KEYSTORE_ZAPEK_STORE_FILE"))
	{
		signingConfigs {
			saker {
				storeFile file(KEYSTORE_ZAPEK_STORE_FILE)
				storePassword KEYSTORE_ZAPEK_STORE_PASSWORD
				keyAlias KEYSTORE_SAKER_KEY_ALIAS
				keyPassword KEYSTORE_SAKER_KEY_PASSWORD
			}
		}
	}
	compileSdkVersion 33

	/* default configuration */
	defaultConfig {
		minSdkVersion 16
		targetSdkVersion 33
		vectorDrawables.useSupportLibrary = "true"
		buildConfigField "boolean", "logging", "true"
		buildConfigField "String", "ACRAUri", "\"\""
		buildConfigField "boolean", "enableDonations", "false"
		resValue "string", "development_url", ""
		resValue "string", "slogan", ""
		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
		resConfigs 'en'
/* limit resource usage to just english (for now) */
	}
	buildTypes {
		debug {
			minifyEnabled false
			shrinkResources false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
		release {
			minifyEnabled true
			shrinkResources true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
			buildConfigField "boolean", "logging", "false"
		}
		/* 'beta' is not for the Play Store, only private betas */
		beta {
			minifyEnabled false
			shrinkResources false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
			applicationIdSuffix ".beta"
			versionNameSuffix "-beta"
		}
	}
	productFlavors {
		saker {
			applicationId 'is.thesaker.reader'
			if (project.hasProperty("KEYSTORE_ZAPEK_STORE_FILE"))
			{
				signingConfig signingConfigs.saker
			}
			versionCode 8
			versionName "1.5"
			buildConfigField "String", "serverUrl", "\"https://thesaker.is/wp-json/wp/v2/\""
			buildConfigField "String", "ACRAUri", "\"https://zapek.com/report/saker/\""
			buildConfigField "boolean", "enableDonations", "true"
			resValue "string", "app_name", "The Saker"
			resValue "string", "slogan", "Stop the Empire\\'s war on Russia"
			resValue "string", "web_url", "https://thesaker.is"
			resValue "string", "support_email", "vineyardsaker@gmail.com"
			resValue "string", "development_url", "https://zapek.com"
		}
	}
	compileOptions {
		sourceCompatibility = JavaVersion.VERSION_11
		targetCompatibility = JavaVersion.VERSION_11
	}
	namespace 'com.zapek.android.submarinereader'

	// productFlavors are run before buildTypes hence they don't use the final
	// applicationId. This final loop fixes it
	applicationVariants.all { variant ->
		variant.buildConfigField "String", "providerAuthority", "\"${applicationId}\""
		variant.resValue "string", "application_id", applicationId
	}
}

dependencies {
	implementation fileTree(include: ['*.jar'], dir: 'libs')
	implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.1.0'
	implementation 'androidx.appcompat:appcompat:1.5.1'
	implementation 'com.google.android.material:material:1.6.1'
	implementation 'androidx.cardview:cardview:1.0.0'
	implementation 'androidx.work:work-runtime:2.7.1'
	implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
	implementation 'com.squareup.okhttp3:okhttp:4.10.0'
	implementation 'com.squareup.retrofit2:retrofit:2.9.0'
	implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
	implementation 'com.google.code.gson:gson:2.8.6'
	implementation 'org.jsoup:jsoup:1.15.3'
	implementation 'com.squareup.picasso:picasso:2.8'
	implementation 'com.android.billingclient:billing:5.0.0'
	betaImplementation 'ch.acra:acra:4.9.2'
	testImplementation 'junit:junit:4.13.2'
	androidTestImplementation('androidx.test.espresso:espresso-core:3.2.0', {
		exclude group: 'com.android.support', module: 'support-annotations'
	})
}
